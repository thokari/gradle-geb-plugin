apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'ivy-publish'
apply plugin: 'idea'
apply plugin: 'eclipse'

repositories { jcenter() }

dependencies {
	compile gradleApi()

	def gradleDowloadDep = 'de.undercouch:gradle-download-task:3.1.0'
	def phantomJsDriverDep = "com.codeborne:phantomjsdriver:${phantomJsDriverVersion}"
	def gebDep = "org.gebish:geb-core:${gebVersion}"

	compile "org.seleniumhq.selenium:selenium-support:${seleniumVersion}"

	compile gradleDowloadDep
	compile(phantomJsDriverDep) {  transitive = false }
	compile gebDep

	runtime gradleDowloadDep
	runtime(phantomJsDriverDep) {  transitive = false }
	runtime gebDep
}

// gradle pulls in groovy dependency
configurations {
    all*.exclude group: 'org.codehaus.groovy', module: 'groovy-all'
}

task sourceJar(type: Jar) {
	from sourceSets.main.java
	classifier = 'sources'
}

group = 'de.thokari'
version = '0.6-SNAPSHOT'

publishing {
	publications {
		ivy(IvyPublication) {
			from components.java
			artifact sourceJar
		}
		maven(MavenPublication) {
			from components.java
			artifact sourceJar

			project.configurations[JavaPlugin.RUNTIME_CONFIGURATION_NAME].allDependencies.findAll {
				it instanceof ModuleDependency && !it.excludeRules.isEmpty()
			}.each { ModuleDependency dep ->
				pom.withXml {
					def xmlDep = asNode().dependencies.dependency.find {
						it.groupId[0].text() == dep.group && it.artifactId[0].text() == dep.name
					}
					def xmlExclusions = xmlDep.exclusions[0]
					if (!xmlExclusions) xmlExclusions = xmlDep.appendNode('exclusions')

					dep.excludeRules.each { ExcludeRule rule ->
						def xmlExclusion = xmlExclusions.appendNode('exclusion')
						xmlExclusion.appendNode('groupId', rule.group)
						xmlExclusion.appendNode('artifactId', rule.module)
					}
				}
			}
		}
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.14'
}
